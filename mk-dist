#!/usr/bin/env zsh
setopt ERR_EXIT NO_UNSET

package_private_name=ldvsoft-env
sourcedir=${0:h}

function error {
	echo 'Error:' $@ >&2
	exit 1
}

debug=0
installopts=()
packages=()
user_abs_home=
function {
	local metas=($sourcedir/*/meta.zsh(.))
	all_packages=(${metas:h:t})
}

while (( $# != 0 )) {
	option=$1
	if [[ $option =~ '^[^-]\.*' && ! -v option_wants_arg ]] {
		break
	}
	shift
	if [[ -v option_wants_arg ]] {
		option=$option_wants_arg=$option
		unset option_wants_arg
	}
	case $option {
		-v|--verbose)
			debug=$((debug + 1))
			;;
		-q|--quiet)
			debug=$((debug - 1))
			;;

		--strict)
			strict=1
			;;
		--no-strict)
			unset strict
			;;

		--build-dir)
			option_wants_arg=$option
			;;
		--build-dir=*)
			param=${option#--*=}
			builddir=${param:P}
			;;
		--no-build-dir)
			unset builddir
			;;

		--no-output)
			unset dist_output
			;;
		--output)
			option_wants_arg=$option
			;;
		--output=*)
			param=${option#--*=}
			dist_output=$param
			;;

		--no-install)
			unset install
			;;
		--install)
			install=1
			;;

		--install-dir)
			option_wants_arg=$option
			;;
		--install-dir=*)
			param=${option#--*=}
			installopts+=--dest-dir=$param
			;;

		--install-mkdir)
			installopts+=--mkdir
			;;

		--location)
			option_wants_arg=$option
			;;
		--location=*)
			param=${option#--*=}
			location=$param
			;;

		--user-absolute-home)
			option_wants_arg=$option
			;;
		--user-absolute-home=*)
			param=${option#--*=}
			user_abs_home=${~param}
			;;

		--uber-package)
			unset packages
			;;
		--package)
			option_wants_arg=$option
			;;
		--package=*)
			if [[ ! -v packages ]] {
				error Cannot combine --package with --uber-package.
			}
			param=${option#--*=}
			if (( ! $all_packages[(Ie)$param] )) {
				error Uknown package $param.
			}
			packages+=$param
			;;

		*)
			error Unknown option $option.
	}
}

function echo-info {
	if (( debug < 1 )) { return }
	echo '[info]' $@ >&2
}

function echo-debug {
	if (( debug < 2 )) { return }
	echo '[debug]' $@ >&2
}

function warning {
	if [[ -v strict ]] {
		error $@
	} else {
		echo 'Warning:' $@ >&2
	}
}

if (( $# != 0 )) {
	error Extra options: $@.
}

if [[ ! -v location ]] {
	error Location not specified.
}

if [[ ! -v packages ]] {
	# Uber
	packages=($all_packages)
	echo-info Detected packages: $packages.
}

if (( $#packages == 0 )) {
	error Package to build not specified. Use --uber-package or --package.
}

if [[ $location != user-* && -n $user_abs_home ]] {
	warning --user-absolute-home option is only effective for user-… locations.
}

if [[ ! -v install && $#installopts != 0 ]] {
	warning --install-… options are only effective when --install is enabled.
}

function location_set_private_stem {
	case $1 {
		public)
			location_private_stem=$package_private_name
			;;
		private)
			;;
		*)
			error Wrong parameter $2.
			;;
	}
}

function location_absolute {
	location_kind=absolute
	location_prefix=${1%/}
	dist_location_prefix=

	location_set_private_stem $2
}

function location_user {
	if [[ -n $user_abs_home ]] {
		location_absolute ${user_abs_home}/$1 $2
		return
	}

	location_kind=user-home-adaptive
	location_prefix='${HOME}'/${1%/}
	dist_location_prefix='${HOME}'

	location_set_private_stem $2
}

function location_test {
	location_kind=test
	location_prefix=${${:-$sourcedir/test-install}:P}
	dist_location_prefix=$location_prefix
}

case $location {
	system-root)
		location_absolute '' public
		;;
	system-usr)
		location_absolute /usr public
		;;
	system-local)
		location_absolute /usr/local public
		;;
	system-opt)
		location_absolute /opt/$package_private_name private
		;;
	user-xdg)
		error Location $param is not implemented.
		;;
	user-local)
		location_user .local public
		;;
	user-opt)
		location_user .local/opt/$package_private_name private
		;;
	absolute@*)
		location_absolute ${${location#absolute@}:P} private
		;;
	test-install)
		location_test
		;;
	*)
		echo Unknown location $location. >&2
		exit 1
}

location_bindir=$location_prefix/bin
location_libdir_public=$location_prefix/lib
location_libdir_private=$location_libdir_public/${location_private_stem-private}
location_execdir_public=$location_prefix/libexec
location_execdir_private=$location_execdir_public/${location_private_stem-private}
# TODO: expression support for user-xdg
if [[ ! -v location_datadir_private ]] {
	location_datadir_private=$location_prefix/share${location_private_stem+/$location_private_stem}
}

dist_location_bindir=${location_bindir#$dist_location_prefix/}
dist_location_libdir_public=${location_libdir_public#$dist_location_prefix/}
dist_location_libdir_private=${location_libdir_private#$dist_location_prefix/}
dist_location_execdir_public=${location_execdir_public#$dist_location_prefix/}
dist_location_execdir_private=${location_execdir_private#$dist_location_prefix/}
if [[ ! -v dist_location_datadir_private ]] {
	dist_location_datadir_private=${location_datadir_private#$dist_location_prefix/}
}

unfunction location_absolute
unfunction location_user
unfunction location_test

if (( debug >= 1 )) {
	echo-info 'Location:' $location "($location_kind)."
	echo-info '|- Prefix:           ' $location_prefix/
	echo-info '|- Distib prefix:    ' $dist_location_prefix/
	echo-info '|'
	echo-info '|- Bindir:           ' $location_bindir
	echo-info '|- Execdir (public): ' $location_execdir_public
	echo-info '|- Execdir (private):' $location_execdir_private
	echo-info '|- Libdir (public):  ' $location_libdir_public
	echo-info '|- Libdir (private): ' $location_libdir_private
	echo-info '|- Datadir (private):' $location_datadir_private
}

if [[ ! -v dist_output && ! -v builddir && ! -v install ]] {
	error Nothing to do\; request at least one: \
		$'\n\t'explicit build location via --builddir\; \
		$'\n\t'distribution output via --output\; \
		$'\n\t'immediate installation via --install.
}

echo-info Preparing build.

if [[ ! -v builddir ]] {
	builddir=${$(mktemp -d --tmpdir ldvsoft-env_mk-dist.XXXX)%/}

	TRAPZERR TRAPEXIT () {
		rm -rf $builddir
	}
} else {
	if [[ ! -d $builddir ]] {
		error Build dir $builddir does not exist.
	}
	function {
		setopt LOCAL_OPTIONS NULL_GLOB
		rm -rf $builddir/* $builddir/.*
	}
}

echo-info Build at $builddir.

function build-dir {
	local trg=$builddir/$1

	if [[ -d $trg ]] { return }
	if [[ -v 2 ]] {
		mkdir -p $trg
	} else {
		mkdir $trg
	}
}

function build-file {
	local src=$1
	local trg=$builddir/$2
	if [[ -v 3 ]] {
		local extended=1
	}

	if [[ ! -f $src ]] { return }
	if [[ -f $trg ]] {
		error While building file $src already found built file with same target name.
	}

	cp $src $trg
	sed -i -e ${(ej:;:)replacements} $trg
	if [[ -v extended ]] {
		sed -i -e ${(ej:;:)extended_replacements} $trg
	}
}

function build-fhs-dir {
	local dir=$1
	local trg=$2

	if [[ ! -d $dir ]] { return }

	build-dir $trg +

	function {
		setopt NULL_GLOB
		pushd $dir
		function TRAPEXIT {
			popd
		}

		local f
		for f ( **/* ) {
			echo-debug Looking at $f from $dir.
			if [[ -d $f ]] {
				build-dir $trg/$f
			} elif [[ -f $f ]] {
				build-file $f $trg/$f
			}
		}
	}
}

base_replacements=(
	's:@@BINDIR@@:$location_bindir:g'
	's:@@EXECDIR_PUBLIC@@:$location_execdir_public:g'
	's:@@EXECDIR_PRIVATE@@:$location_execdir_private:g'
	's:@@LIBDIR_PUBLIC@@:$location_libdir_public:g'
	's:@@LIBDIR_PRIVATE@@:$location_libdir_private:g'
	's:@@DATADIR_PRIVATE@@:$location_datadir_private:g'
	's:@@PRIVATE_NAME@@:$package_private_name:g'
	's:@@DEPENDENCIES@@:${(j: :)dependencies-}:g'
)
extended_replacements=(
	's:@@BINDIR!DIST@@:$dist_location_bindir:g'
	's:@@EXECDIR_PUBLIC!DIST@@:$dist_location_execdir_public:g'
	's:@@EXECDIR_PRIVATE!DIST@@:$dist_location_execdir_private:g'
	's:@@LIBDIR_PUBLIC!DIST@@:$dist_location_libdir_public:g'
	's:@@LIBDIR_PRIVATE!DIST@@:$dist_location_libdir_private:g'
	's:@@DATADIR_PRIVATE!DIST@@:$dist_location_datadir_private:g'
	's:@@PACKAGES@@:${(j:, :)packages}:g'
	's:@@LOCATION@@:$location:g'
	's:@@LOCATION_KIND@@:$location_kind:g'
	's:@@DIST_LOCATION_PREFIX@@:$dist_location_prefix:g'
)

replacements=($base_replacements)
build-file $sourcedir/dist-install install +
build-file $sourcedir/dist-README.md README.md +
unset replacements
for p ( $packages ) {
	function {
		echo-info Building package $p…

		local description
		local dependencies=()
		local replacements=($base_replacements)

		function add-replacement {
		replacements+=(s:@@$1@@:$2:g)
		}

		source $sourcedir/$p/meta.zsh

		if [[ $p != core ]] {
			echo-info hiii
			dependencies+=(core)
		}
		unfunction add-replacement
		echo-info '  Dependencies:' $dependencies
		echo-debug Replacements:$'\n ' ${(epj:\n  :)replacements}

		build-fhs-dir $sourcedir/$p/bin          $dist_location_bindir
		build-fhs-dir $sourcedir/$p/lib-public   $dist_location_libdir_public
		build-fhs-dir $sourcedir/$p/lib-private  $dist_location_libdir_private
		build-fhs-dir $sourcedir/$p/exec-public  $dist_location_execdir_public
		build-fhs-dir $sourcedir/$p/exec-private $dist_location_execdir_private
		build-fhs-dir $sourcedir/$p/data         $dist_location_datadir_private
	}
}

unfunction build-dir
unfunction build-file
unfunction build-fhs-dir

if (( debug >= 1 )) (
	cd $builddir
	exec tree -a
)

if [[ -v dist_output ]] {
	tar --create --auto-compress -f $dist_output -C $builddir .
	echo Created distribution archive $dist_output.
}

if [[ ! -v install ]] {
	exit
}

echo-info Executing install.

$builddir/install $installopts

echo Installation done.
