#!/usr/bin/env zsh
setopt ERR_EXIT NULL_GLOB EXTENDED_GLOB NO_UNSET

while (( $# != 0 )) {
	option=$1
	if [[ $option =~ '^[^-]\.*' && ! -v option_wants_arg ]] {
		break
	}
	shift
	if [[ -v option_wants_arg ]] {
		option=$option_wants_arg=$option
		unset option_wants_arg
	}
	case $option {
		--dest-dir)
			option_wants_arg=$option
			;;
		--dest-dir=*)
			param=${option#--*=}
			DESTDIR=$param
			;;

		--mkdir)
			mkdir_public=1
			;;
		--no-mkdir)
			unset mkdir_public
			;;

		*)
			echo Unknown option $option. >&2
			exit 1
	}
}

if (( $# != 0 )) {
	echo Extra options: $@. >&2
	exit 1
}

cmdprefix=(${(@)CMDPREFIX:-})
installer=($cmdprefix cp -r --preserve=timestamps)
sourcedir=${0:h}

function install-dir {
	local dir=${1#/}
	local installdir=${2#/}
	local mode=$3
	local exclude=${4-/CANNOT-MATCH-A-THING}

	if [[ ! -d $sourcedir/$dir ]] { return }

	local src=$sourcedir/$dir
	local trg=${${DESTDIR:-}%/}/$installdir

	case $mode {
		public)
			local exclude=$sourcedir/${exclude#/}
			[[ -d $trg ]] || [[ -v mkdir_public ]] && mkdir -p $trg
			targets=($src/*~$exclude)
			if (( $#targets != 0 )) {
				$installer $targets $trg
			}
			;;
		private)
			[[ -d ${trg:h} ]] || [[ -v mkdir_public ]] && mkdir -p ${trg:h}
			[[ -d $trg ]] || $cmdprefix mkdir $trg
			targets=($src/*)
			if (( $#targets != 0 )) {
				$installer $targets $trg
			}
			;;
		*)
			exit 1
			;;
	}
}

install-dir '@@BINDIR!DIST@@'          "@@BINDIR@@"          public
install-dir '@@LIBDIR_PUBLIC!DIST@@'   "@@LIBDIR_PUBLIC@@"   public  "@@LIBDIR_PRIVATE!DIST@@"
install-dir '@@LIBDIR_PRIVATE!DIST@@'  "@@LIBDIR_PRIVATE@@"  private
install-dir '@@EXECDIR_PUBLIC!DIST@@'  "@@EXECDIR_PUBLIC@@"  public  "@@EXECDIR_PRIVATE!DIST@@"
install-dir '@@EXECDIR_PRIVATE!DIST@@' "@@EXECDIR_PRIVATE@@" private
install-dir '@@DATADIR_PRIVATE!DIST@@' "@@DATADIR_PRIVATE@@" private
